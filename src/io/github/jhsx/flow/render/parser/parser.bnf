{

  parserClass='io.github.jhsx.flow.render.parser.FlowRenderParser'
  parserUtilClass="io.github.jhsx.flow.render.parser.FlowRenderParserUtil"


  elementTypeHolderClass='io.github.jhsx.flow.render.FlowRenderTypes'
  elementTypeClass='io.github.jhsx.flow.render.psi.FlowRenderCompositeElementType'
  tokenTypeClass='io.github.jhsx.flow.render.psi.FlowRenderTokenType'

  psiClassPrefix='FlowRender'
  psiImplClassSuffix='Impl'
  psiPackage='io.github.jhsx.flow.render.psi'
  psiImplPackage='io.github.jhsx.flow.render.psi.impl'
//  psiImplUtilClass="io.github.jhsx.flow.render.psi.impl.FlowRenderPsiImplUtil"
  parserUtilClass="io.github.jhsx.flow.render.parser.FlowRenderParserUtil"
  extends(".*Expr")=Expression
  extends(".*Statement")=Statement
  consumeTokenMethod(".*Expr")="consumeTokenFast"

  tokens = [
    COMMENT              =  ''
    LDOUBLE_BRACE        =  '{{'
    RDOUBLE_BRACE        =  '}}'
    LBRACE               =  '{'
    RBRACE               =  '}'
    LPAREN               =  '('
    RPAREN               =  ')'
    EQ                   =  '=='
    ASSIGN               =  '='
    SCOPE_ASSIGN         =  ':='
    COLON                =  ':'
    COLONCOMMA           =  ';'
    NOT_EQ               =  '!='
    NOT                  =  '!'
    COND_OR              =  '||'
    COND_AND             =  '&&'
    BIT_OR               =  '|'
    LESS_OR_EQUAL        =  '<='
    LESS                 =  '<'
    GREATER_OR_EQUAL     =  '>='
    GREATER              =  '>'
    DOT                  =  '.'
    COMMA                =  ','
    MUL                  = '*'
    DIV                  = '/'
    MOD                  = '%'
    PLUS                 = '+'
    MINUS                = '-'
    TERNARY              = '?'
    IF                   = 'if'
    END                  = 'end'
    ELSE                 = 'else'
    BLOCK                = 'block'
    RANGE                = 'range'
    YIELD                = 'yield'
    EXTENDS              = 'extends'
    INCLUDE              = 'include'
    IMPORT               = 'import'
  ]

   pin("(.)+Statement$") = 2
}

File ::= ExtendsStatement? ImportStatement*  StatementList

ExtendsStatement ::= '{{' 'extends' StringExpr '}}'
ImportStatement  ::= '{{' 'import' StringExpr '}}'

StatementList    ::= ( TEXT+ | IncludeStatement | IfStatement | BlockStatement | RangeStatement | YieldStatement | PipelineStatement )*

fake Statement ::=


FieldChain      ::= '.' IDENT FieldChain?
FieldExpr       ::= FieldChain
FieldChainExpr  ::= Expression FieldChain

Expression ::=        OrExpr
                    | AndExpr
                    | ConditionalExpr
                    | MultiplicativeExpr
                    | AdditiveExpr
                    | CallExpr
                    | FieldExpr
                    | FieldChainExpr
                    | NotExpr
                    | TernaryExpr
                    | PrimaryGroup

TernaryExpr ::= Expression '?' Expression ':' Expression

private rel_op  ::= '==' | '!=' | '<' | '<=' | '>' | '>='
private PrimaryGroup ::= NumberExpr  | IdentifierExpr | StringExpr | ParenthesesExpr
CallExpr ::= Expression '(' expression_list')' {pin=2}

private add_op  ::= '+' | '-'
private mul_op  ::= '*' | '%' | '/'
NotExpr         ::= '!' Expression { pin=1 }
OrExpr          ::= Expression '||' Expression
AndExpr         ::= Expression '&&' Expression
ConditionalExpr ::= Expression rel_op Expression
MultiplicativeExpr ::= Expression mul_op Expression
AdditiveExpr    ::= Expression add_op Expression

StringExpr     ::= RAW_STRING|STRING
IdentifierExpr ::= IDENT | '.'
NumberExpr     ::= NUMBER
ParenthesesExpr   ::= '(' Pipeline ')' {pin=1}

Pipeline      ::= Expression (':' expression_list)?  Pipe? {pin=1}
Pipe          ::= '|' Expression (':' expression_list)? Pipe? {pin=1}

private expression_list ::= (Expression (',' Expression)*)?

private assign_op ::= '=' | ':='

private set_declaration ::= (fieldOrID (',' fieldOrID)*)  assign_op Pipeline (',' Pipeline)*
IfStatement       ::= '{{' 'if' (set_declaration ';')? Pipeline '}}' StatementList (EndStatement|ElseIfStatement|ElseStatement)
ElseIfStatement   ::= '{{' 'else' 'if' (set_declaration ';')? Pipeline '}}' StatementList (EndStatement|ElseIfStatement|ElseStatement) { pin=3}
ElseStatement     ::= '{{' 'else' '}}' StatementList EndStatement
EndStatement      ::= '{{' 'end' '}}'
RangeStatement    ::= '{{' 'range' (fieldOrID (',' fieldOrID)? assign_op)? Pipeline '}}' StatementList (EndStatement|ElseStatement)
private fieldOrID ::= FieldChainExpr|IdentifierExpr


PipelineStatement ::= '{{' (set_declaration ';')? Pipeline '}}' { pin=3}
IncludeStatement  ::= '{{' 'include' StringExpr  (set_declaration ';')? Pipeline? '}}'
BlockStatement    ::= '{{' 'block' IdentifierExpr  (set_declaration ';')? Pipeline? '}}' StatementList EndStatement
YieldStatement    ::= '{{' 'yield' IdentifierExpr (set_declaration ';')? Pipeline? '}}'

